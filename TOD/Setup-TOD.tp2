BACKUP ~TOD/Backup~
AUTHOR ~Valiant (kiski@centrum.sk)~
VERSION ~v5.0.1~

AUTO_TRA ~TOD/Tra/%s~

ALWAYS

  ACTION_DEFINE_ARRAY va#reload BEGIN setup END

  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charsets = 1
    STR_VAR
      tra_path = EVAL ~%MOD_FOLDER%/tra~
      reload_array = va#reload
  END
END


LANGUAGE ~English~                                                        ~English~ ~TOD/Tra/English/setup.tra~

LANGUAGE ~Castellano (traducido por Clan REO y RPG-Bardo.com)~            ~Spanish~ ~TOD/Tra/Spanish/setup.tra~

LANGUAGE ~Czech (prelozil Vlasak a Jab, www.sigil.cz)~                    ~Czech~   ~TOD/Tra/Czech/setup.tra~

LANGUAGE ~Russian (translation Austin&Tanger Soto&www.aerie.ru)~          ~Russian~ ~TOD/Tra/Russian/setup.tra~

LANGUAGE ~Italian (translated by ilot)~                                   ~Italian~ ~TOD/Tra/Italian/setup.tra~

LANGUAGE ~German (translated by Gerri)~                                   ~German~  ~TOD/Tra/German/setup.tra~

LANGUAGE ~Francais (traduit par Le Marquis)~                              ~French~  ~TOD/Tra/French/setup.tra~

LANGUAGE ~Polish (translated by Grzebul and Cuttooth)~                    ~Polish~  ~TOD/Tra/Polish/setup.tra~

BEGIN @100
REQUIRE_PREDICATE GAME_IS ~BGT TOB BG2EE EET~ @101

APPEND ~MASTAREA.2da~ ~va#mov  value~
APPEND ~MASTAREA.2da~ ~va#001  value~

COPY ~TOD/Are~ 		~override~
COPY ~TOD/Bmp~		~override~
COPY ~TOD/Spl~ 		~override~
COPY ~TOD/Wav~ 		~override~

ACTION_IF GAME_IS ~BG2EE EET~ THEN BEGIN
  COPY ~TOD/PVRZ~               ~override~
  END
  
APPEND ~State.ids~ ~0x8010202D RZ_STATE_DISABLED~    UNLESS ~0x8010202D[ %TAB%]+RZ_STATE_DISABLED~

APPEND ~State.ids~ ~0x49C08000 RZ_STATE_DISPEL~     UNLESS ~0x49C08000[ %TAB%]+RZ_STATE_DISPEL~

APPEND ~State.ids~ ~0x00000029 RZ_STATE_VULNERABLE~   UNLESS ~0x00000029[ %TAB%]+RZ_STATE_VULNERABLE~

COMPILE ~TOD/Baf~

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~ar0004.bcs~) THEN BEGIN
    COMPILE ~ar0004.bcs~ ~TOD/AppendBaf/ar0004.baf~
  END ELSE BEGIN
    EXTEND_TOP ~ar0004.bcs~ ~TOD/AppendBaf/ar0004.baf~
  END

EXTEND_TOP ~ar0021.bcs~ ~TOD/AppendBaf/ar0004.baf~

COMPILE ~TOD/Dialog~

  COPY ~TOD/Itm/va#book.itm~     ~override/va#book.itm~
    SAY NAME1 @200
    SAY NAME2 @201
    SAY UNIDENTIFIED_DESC @202
    SAY DESC @203

  COPY ~TOD/Itm/va#calan.itm~    ~override/va#calan.itm~
    SAY NAME2 @300
    SAY DESC @301

  ACTION_FOR_EACH res IN va#hring va#iring va#gring va#lich rzdispch rzseeinv BEGIN
    COPY ~TOD/Itm/%res%.itm~     ~override/%res%.itm~
  END

  COPY ~TOD/Cre/copy~            ~override~

  ACTION_DEFINE_ASSOCIATIVE_ARRAY cre_assoc_array BEGIN
  //uniq-id, name1, name2 => cre
    a, 400, 400 => va#astsd
    b, 401, 401 => va#balor
    c, 403, 404 => va#gulin
    d, 405, 405 => va#kergo
    e, 406, 406 => va#lich
    f, 407, 407 => va#maril
    g, 408, 408 => va#panda
    h, 403, 404 => va#guli2
    i, 409, 409 => va#prisn
    j, 410, 410 => va#ghost
    k, 411, 411 => va#shd01
    l, 412, 412 => va#shdgl
    m, 413, 413 => va#tian
    n, 413, 413 => va#tian2
    o, 414, 414 => va#wiste
    p, 415, 415 => va#zom01
    q, 415, 415 => va#zom02
    r, 415, 415 => va#zom03
    s, 415, 415 => va#zomgl
  END

  ACTION_PHP_EACH cre_assoc_array AS values => res BEGIN
    COPY ~TOD/Cre/%res%.cre~    ~override/%res%.cre~
      SAY NAME1 ( AT "%values_1%" )
      SAY NAME2 ( AT "%values_2%" )
  END

  ACTION_IF GAME_IS ~BG2EE EET~ THEN BEGIN
    ADD_JOURNAL @17 @18 @19  @28  @30  @40 @41 @42 @43 @44 @45 @46 @47 @48 @49
    USING ~TOD/Tra/%LANGUAGE%/setup.tra~ ~TOD/Tra/%LANGUAGE%/va#gulin.tra~ ~TOD/Tra/%LANGUAGE%/va#panda.tra~ ~TOD/Tra/%LANGUAGE%/va#tian.tra~
  END

  ACTION_IF GAME_IS ~BG2EE EET~ THEN BEGIN
    MAKE_BIFF ~VA#Movie~ BEGIN ~TOD/Mve~ ~^.*\.wbm$~ END
  END ELSE BEGIN
    MAKE_BIFF ~VA#Movie~ BEGIN ~TOD/Mve~ ~^.*\.mve$~ END
  END

BEGIN @106
REQUIRE_FILE ~override/va#tod.nul~ @107

  COMPILE ~TOD/AppendBaf/va#maril.baf~   ~override/va#maril.bcs~

BEGIN @108
REQUIRE_FILE ~override/va#tod.nul~ @107

  EXTEND_TOP ~baldur.BCS~ ~TOD\Ustrain2\baldur.BAF~
  EXTEND_TOP ~baldur25.BCS~ ~TOD\Ustrain2\baldur.BAF~

  COMPILE ~TOD/Ustrain2/va#011.baf~
  COMPILE ~TOD/Ustrain2/VA#12sc.baf~
  COMPILE ~TOD/Ustrain2/va#TELE2.baf~
  COMPILE ~TOD/Ustrain2/va#ustrn.baf~
  COMPILE ~TOD/Ustrain2/va#fplan.baf~
  COMPILE ~TOD/Ustrain2/va#gitfm.baf~
  COPY ~TOD/Ustrain2/va#gitfm.cre~    ~override/va#gitfm.cre~
  COPY ~TOD/Ustrain2/va#gitf1.cre~    ~override/va#gitf1.cre~
  COPY ~TOD/Ustrain2/va#gitf2.cre~    ~override/va#gitf2.cre~
  COPY ~TOD/Ustrain2/va#ustrn.cre~    ~override/va#ustrn.cre~
    SAY NAME1 @416
    SAY NAME2 @416
  COPY ~TOD/Ustrain2/va#otsph.spl~    ~override/va#otsph.spl~
  COPY ~TOD/Ustrain2/va#staf1.spl~    ~override/va#staf1.spl~
  COPY ~TOD/Ustrain2/va#staf2.spl~    ~override/va#staf2.spl~

  COMPILE ~TOD/Ustrain2/VA#ustrn.d~

